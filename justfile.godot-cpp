# vi: ft=just
mode := "debug"
flag_target := if mode == "debug" {
  "target=template_debug use_hot_reload=yes generate_bindings=no"
} else if mode == "release" {
  "target=template_release"
} else {
  error("unknown mode: " + mode)
}
scons := if os() == "windows" {
  "../scons.sh"
} else {
  "scons"
}
mingw := if os() == "windows" {
  "mingw_prefix='C:/msys64/mingw64' use_mingw=yes"
} else {
  "mingw_prefix='/' use_mingw=no"
}
use_llvm := "yes"
job := `nproc`

build-linux:
  {{scons}} -j{{job}} {{flag_target}} use_llvm={{use_llvm}} gdextension_dir=../src/api platform=linux arch=x86_64

build-android-arm32:
  {{scons}} -j{{job}} {{flag_target}} use_llvm={{use_llvm}} gdextension_dir=../src/api platform=android arch=arm32
build-android-arm64:
  {{scons}} -j{{job}} {{flag_target}} use_llvm={{use_llvm}} gdextension_dir=../src/api platform=android arch=arm64

build-android-x86_32:
  {{scons}} -j{{job}} {{flag_target}} use_llvm={{use_llvm}} gdextension_dir=../src/api platform=android arch=x86_32
build-android-x86_64:
  {{scons}} -j{{job}} {{flag_target}} use_llvm={{use_llvm}} gdextension_dir=../src/api platform=android arch=x86_64

build-windows-x86_32:
  {{scons}} {{flag_target}} use_llvm=no gdextension_dir=../src/api {{mingw}} platform=windows arch=x86_32
build-windows-x86_64:
  {{scons}} {{flag_target}} use_llvm=no gdextension_dir=../src/api {{mingw}} platform=windows arch=x86_64

build-android: build-android-arm32 build-android-arm64 build-android-x86_32 build-android-x86_64

build-windows: build-windows-x86_32 build-windows-x86_64

build: build-linux build-android build-windows

